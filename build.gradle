/*
 * Copyright (c) 2021 TraceTronic GmbH
 *
 * SPDX-License-Identifier: MIT
 */
import org.gradle.plugins.ide.idea.model.IdeaLanguageLevel

plugins {
    id 'groovy'
    id 'idea'
}

group 'de.tracetronic.jenkins'
description 'This is a shared library to extend Jenkins pipelines and to keep code DRY.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        groovy {
            srcDirs = ['src', 'vars']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['test/resources']
        }
    }
}

repositories {
    jcenter()
    maven {
        url 'https://repo.jenkins-ci.org/releases/'
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.6'
    compile 'org.jenkins-ci.main:jenkins-core:2.263.4'
    compile 'org.jenkins-ci.plugins.workflow:workflow-api:2.40@jar'
    compile 'org.jenkins-ci.plugins.workflow:workflow-support:3.7@jar'
    compile 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-rest-api:2.19@jar'
    compile 'com.cloudbees:groovy-cps:1.32@jar'

    testCompile 'com.lesfurets:jenkins-pipeline-unit:1.8'
    testCompile 'org.testcontainers:spock:1.15.2'
    testCompile 'org.hamcrest:hamcrest-core:2.2@jar'
    testCompile 'org.jenkins-ci.main:jenkins-war:2.263.4'
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.71'
    testCompile 'org.jenkins-ci.plugins:structs:1.21@jar'
    testCompile 'org.jenkins-ci.plugins.workflow:workflow-api:2.40@jar'
    testCompile 'org.jenkins-ci.plugins.workflow:workflow-cps:2.41@jar'
    testCompile 'org.jenkins-ci.plugins.workflow:workflow-job:2.40@jar'
    testCompile 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.13@jar'
    testCompile 'org.powermock:powermock-core:2.0.9'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
}

test {
    filter {
        includeTestsMatching('*Test')
    }
    dependsOn cleanTest
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

task integrationTest(type: Test) {
    filter {
        includeTestsMatching('*IT')
    }
    dependsOn cleanTest
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

idea {
    project {
        vcs = 'Git'
        jdkName = '1.8'
        languageLevel = '8'
    }
    module {
        jdkName = '1.8'
        languageLevel = new IdeaLanguageLevel('8')
        downloadJavadoc = true
        downloadSources = true
    }
}

wrapper {
    gradleVersion = '6.8'
}
